plugins {
	id "fabric-loom" version "0.2.0-SNAPSHOT"
	id "com.matthewprenger.cursegradle" version "1.1.2"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "${mod_name}"
version = "${mod_version}"

minecraft {
	
}

jar {
	manifest
	{
		attributes "Implementation-Title": project.archivesBaseName
		attributes "Implementation-Version": project.version
		attributes "Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}".toLowerCase(Locale.ROOT)
		attributes "Built-On-Minecraft": "${project.mc_version}"
		attributes "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
	}
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	manifest
	{
		attributes "Implementation-Title": project.archivesBaseName + "-deobf"
		attributes "Implementation-Version": project.version
		attributes "Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}:deobf".toLowerCase(Locale.ROOT)
		attributes "Built-On-Minecraft": "${project.mc_version}"
		attributes "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
	}
	classifier = "deobf"
}

dependencies {
	minecraft "com.mojang:minecraft:${mc_version}"
	mappings "net.fabricmc:yarn:${yarn_mappings}"
	
	modCompile "net.fabricmc:fabric-loader:${loader_version}"
	modCompile "net.fabricmc:fabric:${fabric_version}"
	
	compile "com.google.code.findbugs:jsr305:${findbugs_version}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	from sourceSets.main.allSource
	classifier = "sources"
}

curseforge {
	if(project.hasProperty("curseforgeAPIKey")) {
		apiKey = project.properties.curseforgeAPIKey
	}
	
	if(project.hasProperty("curseforge_id")) {
		project {
			id = project.properties.curseforge_id
			addGameVersion "${release_version}"
			mainArtifact(jar) {
				displayName = "${mod_name}-${mc_version}-${mod_version}.jar"
				relations {
					requiredDependency "fabric"
				}
			}
			
			releaseType = project.release_type
			addGameVersion project.release_version
			
			addArtifact(deobfJar) {
				displayName = "${mod_name}-${mc_version}-${mod_version}-dev.jar"
			}
		}
		options {
			forgeGradleIntegration = false
		}
	}
}

afterEvaluate {
	if(project.hasProperty("curseforge_id")) {
		tasks.each {
			if(it.name == "curseforge${curseforge_id}") {
				it.dependsOn build
				it.mustRunAfter build
			}
		}
	}
}


