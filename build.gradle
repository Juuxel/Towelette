plugins {
	id "fabric-loom" version "0.2.0-SNAPSHOT"
	id "com.matthewprenger.cursegradle" version "1.1.2"
	id "maven-publish"
}

repositories {
	mavenLocal()
	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven"
	}
	maven {
		name = "JitPack"
		url = "https://jitpack.io"
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "${mod_name}"
version = "${mod_version}"

minecraft {
	
}

dependencies {
	minecraft "com.mojang:minecraft:${mc_version}"
	mappings "net.fabricmc:yarn:${yarn_mappings}"
	
	modCompile "net.fabricmc:fabric-loader:${loader_version}"
	modCompile "net.fabricmc:fabric:${fabric_version}"
	
	compileOnly "com.google.code.findbugs:jsr305:${findbugs_version}"
}

processResources {
	inputs.property "version", project.version
	
	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		
		expand "version": project.version
	}
	
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

jar {
	manifest
	{
		attributes "Implementation-Title": project.archivesBaseName
		attributes "Implementation-Version": project.version
		attributes "Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}"
		attributes "Built-On-Minecraft": "${project.mc_version}"
		attributes "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
	}
	classifier = ""
}

task devJar(type: Jar) {
	from sourceSets.main.output
	manifest
	{
		attributes "Implementation-Title": project.archivesBaseName + "-dev"
		attributes "Implementation-Version": project.version
		attributes "Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}:dev"
		attributes "Built-On-Minecraft": "${project.mc_version}"
		attributes "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
	}
	classifier = "dev"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	from sourceSets.main.allSource
	manifest
	{
		attributes "Implementation-Title": project.archivesBaseName + "-sources"
		attributes "Implementation-Version": project.version
		attributes "Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}:sources"
		attributes "Built-On-Minecraft": "${project.mc_version}"
		attributes "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
	}
	classifier = "sources"
}

publishing {
	tasks.publish.dependsOn build
	publications {
		mavenJava(MavenPublication) {
			artifact jar
			artifact devJar
			artifact sourcesJar
		}
	}
	
	repositories {
		mavenLocal()
	}
}

String generateHTMLChangelog() {
	String changelog_text = ""
	File file = file("changelog.txt")
	if(file.exists())
	{
		file.eachLine { String line ->
			if(!line.isAllWhitespace())
			{
				changelog_text += "<p>${line.stripIndent()}</p>\n"
			}
		}
		changelog_text += "<p>&nbsp;</p>\n"
	}
	changelog_text += "<p>Last successfully tested on:</p>\n<p>Minecraft version ${mc_version}</p>\n<p>Yarn mappings ${yarn_mappings}</p>\n<p>Fabric-Loader ${loader_version}</p>\n<p><a href=\"https://minecraft.curseforge.com/projects/fabric/files/\" rel=\"nofollow\">Fabric API ${fabric_version}</a></p>\n"
	return changelog_text
}

curseforge {
	if(project.hasProperty("curseforge_api_key")) {
		apiKey = project.properties.curseforge_api_key
	}
	
	if(project.hasProperty("curseforge_id")) {
		project {
			id = project.properties.curseforge_id
			addGameVersion "${release_version}"
			mainArtifact(jar) {
				displayName = "${mod_name}-${mc_version}-${mod_version}.jar"
				relations {
					requiredDependency "fabric"
				}
				
				changelogType = "html"
				changelog = project.generateHTMLChangelog()
			}
			
			releaseType = project.release_type
			addGameVersion project.release_version
			
			addArtifact(devJar) {
				displayName = "${mod_name}-${mc_version}-${mod_version}-dev.jar"
			}
			
			addArtifact(sourcesJar) {
				displayName = "${mod_name}-${mc_version}-${mod_version}-sources.jar"
			}
		}
		options {
			forgeGradleIntegration = false
		}
	}
}

afterEvaluate {
	if(project.hasProperty("curseforge_id")) {
		tasks.each {
			if(it.name == "curseforge${curseforge_id}") {
				it.dependsOn build
				it.mustRunAfter build
			}
		}
	}
}


